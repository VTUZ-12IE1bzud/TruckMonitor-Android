apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "${rootDir}/tools/script-git-version.gradle"

def versionMajor = 0
def versionMinor = 0
def versionPatch = 1
def versionBuild = 0

android {

    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "ru.annin.truckmonitor"
        minSdkVersion 21
        targetSdkVersion 25

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}" + (versionPatch == 0 ? "" : ".${versionPatch}" + "-${gitRevision}")

        resConfigs "en", "ru", "zh"
        multiDexEnabled true
        testInstrumentationRunner "ru.annin.truckmonitor.MultiDexAndroidJUnitRunner"
    }

    productFlavors {
        dev {
            buildConfigField "String", "API_BASE_URL", '"http://192.168.1.2:8080"'
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "D"
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    applicationVariants.all { variant ->
        File outDir = file("${rootDir}/out/${variant.buildType.name}")
        if (!outDir.exists()) {
            outDir.mkdirs()
        }
        variant.assemble.doLast {
            variant.outputs.each { output ->
                File apkFile = output.outputFile
                copy {
                    from apkFile
                    into outDir
                    rename { fileName -> "TruckMonitor_v${variant.versionName}.${variant.versionCode}-${variant.flavorName}.apk" }
                }
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Android Support Library
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:multidex:1.0.1'

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // MVP
    compile 'com.arello-mobile:moxy:1.5.1'
    compile 'com.arello-mobile:moxy-app-compat:1.5.1'
    kapt 'com.arello-mobile:moxy-compiler:1.5.1'

    // Rest Api
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.7.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.squareup.retrofit2:converter-jackson:2.1.0'
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.8.8"

    // Rx
    compile 'io.reactivex:rxjava:1.2.10'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxkotlin:1.0.0'

    // Test
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support:support-annotations:25.3.1'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

kapt {
    generateStubs = true
}